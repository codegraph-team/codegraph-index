apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

targetCompatibility = 1.6
sourceCompatibility = 1.6

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.2.2'

    testCompile libraries.unitTesting
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['ClientArtifact']
    publish = project.releaseType != "RELEASE"
    override = project.releaseType == "BUILD-SNAPHSHOT"

    pkg {
        userOrg = 'codegraph'
        repo = project.publishRepository
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dnfeitosa/codegraph-index'
        name = project.name

        version {
            name = project.version
        }
    }
}

publishing {
    publications {
        ClientArtifact(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}
