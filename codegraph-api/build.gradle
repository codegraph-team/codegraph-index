apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'java'
apply plugin: 'idea'

dependencies {
  compile project(':coollections')
  compile project(':codegraph-core')
  compile project(':codegraph-db')

  compile group: "org.springframework", name: "spring-core", version: "4.1.0.RELEASE"
  compile group: "org.springframework", name: "spring-webmvc", version: "4.1.0.RELEASE"
  compile group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.3.3"
  compile group: "org.codehaus.jackson", name: "jackson-mapper-asl", version: "1.9.13"

  testCompile group: "javax.servlet", name: "servlet-api", version: "2.5"
  testCompile group: "org.springframework", name: "spring-test", version: "4.1.0.RELEASE"
  testCompile group: "org.neo4j", name: "neo4j-kernel", version: "2.1.3", classifier: "tests"
}

httpPort = 8080
stopPort = 9080
stopKey = 'stopKey'

sourceSets {
    acceptance

    // IntelliJ bug that prevents custom test resources to be added into the
    // classpath when executing JUnit tests. See IDEA-128966
    if (System.properties.'idea.active') {
        main {
            resources.srcDir file('src/acceptance/resources')
        }
    }
}

configurations {
    acceptanceCompile.extendsFrom testCompile
    acceptanceRuntime.extendsFrom testRuntime
}

task acceptanceTests(type: Test) {
    testClassesDir = project.sourceSets.acceptance.output.classesDir
    classpath = project.sourceSets.acceptance.runtimeClasspath
}

build.dependsOn acceptanceTests

